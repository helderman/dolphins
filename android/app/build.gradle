plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    signingConfigs {
        release {
            storeFile file(String.valueOf(System.getenv("KEYSTORE_FILENAME")))
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyPassword System.getenv("KEY_PASSWORD")
            keyAlias System.getenv("KEY_ALIAS")
        }
    }
    compileSdk 34

    defaultConfig {
        applicationId "com.dojadragon.dolphins"
        minSdk 19
        targetSdk 34
        versionCode 15
        versionName "1.0.14"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // The following argument makes the Android Test Orchestrator run its
        // "pm clear" command after each test invocation. This command ensures
        // that the app's state is completely cleared between tests.
        testInstrumentationRunnerArguments clearPackageData: 'true'
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }
    lint {
        abortOnError true
        checkAllWarnings true
        informational 'UnsupportedChromeOsHardware', 'OldTargetApi'
        warningsAsErrors true
    }
    namespace 'com.dojadragon.dolphins'
}

dependencies {
    implementation 'androidx.core:core-ktx:1.13.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.14'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.ext:junit-ktx:1.1.5'
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.3.0'
    androidTestUtil 'androidx.test:orchestrator:1.4.2'
}
